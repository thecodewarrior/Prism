plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.jetbrains.kotlin.jvm' version '1.5.21'
    id 'org.jetbrains.dokka' version '1.5.0'
}

def maven_version = library_version
def snapshotBranch = System.getenv('SNAPSHOT_BRANCH')
if(snapshotBranch != null) {
    if(snapshotBranch.startsWith('refs/heads/'))
        snapshotBranch = snapshotBranch.substring('refs/heads/'.length())
    maven_version = snapshotBranch.replaceAll('[^.\\w-]', '-') + '-SNAPSHOT'
    logger.lifecycle('Using SNAPSHOT version `{}`', version)
}

// pass `-PprismVersion=foo` to set the version directly. Useful for publishing to the local maven repository
if(hasProperty('prismVersion')) {
    maven_version = prismVersion
}

group = 'dev.thecodewarrior'
archivesBaseName = 'prism'
version = maven_version

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    api 'org.jetbrains.kotlin:kotlin-stdlib'
    api 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    api 'org.jetbrains.kotlin:kotlin-reflect'

    api 'dev.thecodewarrior:mirror:1.0.0b2'
    api 'org.apache.logging.log4j:log4j-api:2.14.1'

    testImplementation 'dev.thecodewarrior:reflectcase:0.1.0'
    testImplementation 'org.apache.logging.log4j:log4j-core:2.14.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
}

kotlin {
    explicitApi = 'warning'
}

configure([compileKotlin, compileTestKotlin]) {
    kotlinOptions {
        jvmTarget = '1.8'
        javaParameters = true
        freeCompilerArgs += '-Xjvm-default=all'
        freeCompilerArgs += '-Xopt-in=kotlin.contracts.ExperimentalContracts'
    }
}

configure([compileJava, compileTestJava]) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    options.compilerArgs.add('-parameters')
}

test {
    useJUnitPlatform()
}

// publishing

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    dependsOn(dokkaJavadoc)
    from(dokkaJavadoc.outputDirectory)
}

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'prism'

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Prism'
                description = 'Format-agnostic reflective (de)serialization backbone'
                url = 'https://github.com/thecodewarrior/Prism'
                licenses {
                    license {
                        name = 'BSD-2-Clause'
                        url = 'https://opensource.org/licenses/BSD-2-Clause'
                    }
                }
                developers {
                    developer {
                        id = 'thecodewarrior'
                        name = 'Pierce Corcoran'
                        email = 'code@thecodewarrior.dev'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/thecodewarrior/Prism.git'
                    developerConnection = 'scm:git:ssh://github.com:thecodewarrior/Prism.git'
                    url = 'https://github.com/thecodewarrior/Prism'
                }
            }
        }
    }
    repositories {
        maven {
            def stagingRepo = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotRepo = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = project.version.endsWith('SNAPSHOT') ? snapshotRepo : stagingRepo
            name = 'ossrh'
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    if(System.getenv("SIGNING_KEY") != null) {
        useInMemoryPgpKeys(
                System.getenv("SIGNING_KEY_ID"),
                System.getenv("SIGNING_KEY"),
                System.getenv("SIGNING_KEY_PASSWORD")
        )
    } else {
        useGpgCmd()
    }

    sign publishing.publications.maven
}
